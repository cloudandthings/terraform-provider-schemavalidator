---
page_title: "schemavalidator_validate Data Source - terraform-provider-schemavalidator"
subcategory: ""
description: |-
  Validate a json document against a json-schema https://json-schema.org/ schema file.
---

# schemavalidator_validate (Data Source)

Validate a json document against a [json-schema](https://json-schema.org/) schema file.

## Example Usage

The document and schema arguments only accept strings in json format. Any object of format that can be
converted to json using the Terraform built in [`jsonencode`](https://developer.hashicorp.com/terraform/language/functions/jsonencode) function
will work.    
    
These objects can include Terraform `local` variables, input variables, files loaded from your file system or `outputs` from modules and other workspaces.   
    

```terraform
data "schemavalidator_validate" "test" {
  document = jsonencode({ "test" = "test" })

  schema = jsonencode(
    {
      "$schema"  = "http://json-schema.org/draft-07/schema#"
      "x-$id"    = "https://example.com"
      "type"     = "object"
      "required" = ["test"]
  })
}

data "schemavalidator_validate" "from_fs" {
  document = file("./document.json")
  schema   = file("./schema.json")
}

output "validated" {
  value = data.schemavalidator_validate.test.validated
}
```

## Usage with HTTP Provider

It is common for schemas to be published and made publicly referencable. Instead of building the funtionality
to fetch schemas from URLs, the [HTTP provider](https://registry.terraform.io/providers/hashicorp/http/latest)
can be used to fetch the schema (and document in this case) from wherever it is hosted.     
     
This example also converts a `yaml` document to `json` format to make it compatible with the provider.   
   

```terraform
terraform {
  required_providers {
    http = {
      source  = "hashicorp/http"
      version = "3.2.1"
    }
  }
}

data "http" "k8s_deployment_schema" {
  url = "https://kubernetesjsonschema.dev/v1.14.0/deployment-apps-v1.json"
  request_headers = {
    Accept = "application/json"
  }
}

data "http" "k8s_deployment_example" {
  url = "https://raw.githubusercontent.com/kubernetes/website/main/content/en/examples/controllers/nginx-deployment.yaml"
  request_headers = {
    Accept = "application/json"
  }
}

data "schemavalidator_validate" "k8s_deployment" {
  schema   = data.http.k8s_deployment_schema.response_body
  document = jsonencode(yamldecode(data.http.k8s_deployment_example.response_body))
}

output "validated" {
  value = data.schemavalidator_validate.k8s_service.validated
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `document` (String) Content of a json file
- `schema` (String) Content of a [json-schema](https://json-schema.org/) file

### Read-Only

- `id` (String) Id generated for document and schema hashes
- `validated` (Boolean) True if the document's schema could be validated.